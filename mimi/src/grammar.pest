literal = {(!("$" | "%" | "{" | "}") ~ ANY)+}

identifier = {ASCII_ALPHANUMERIC+}
variable = {"$" ~ (identifier | "{" ~ identifier ~ "}")}

color = { ^"black" | ^"white" | ^"red" | ^"green" | ^"yellow" | ^"blue" | ^"magenta" | ^"cyan" }
fg_color = { color }
bg_color = { "bg_" ~ color }
modifier = { ^"bold" | ^"underline" }
attribute = _{ fg_color | bg_color | modifier }
separator = _{ "," ~ " "* }
style = { " "* ~ attribute ~ (separator ~ attribute)* ~ " "* }
styled = { "%[" ~ style ~ "]{" ~ format_string ~ "}" }

format_string = _{ (literal | variable | styled)+ }
format_string_entire = _{ SOI ~ format_string ~ EOI }
